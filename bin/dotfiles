#!/bin/zsh

dotfiles---help() {
cat << EOF
  usage: dotfiles all|git|node|python|brew|tmux|vim|zsh
EOF
}

dotfiles-temp-fix-path() {
  # it's possible that brew is already installed but the path is not set correctly
  # fix the path for the current script
  if [[ ! $PATH == *"/usr/local/bin"* ]]; then
    path=(/usr/local/bin $path)
    export PATH
  fi

  path=(${DOTFILES_DIRECTORY}/bin $path)
}

dotfiles-pre() {
  dotfiles-temp-fix-path

  if ! command-exists "gcc"; then
    print-error "The XCode Command Line Tools must be installed first."
    printf "  Download them from: https://developer.apple.com/downloads\n"
    printf "  Or install them with: xcode-select --install\n"
    exit 1
  fi

  # install brew
  if ! command-exists "brew"; then
    print-header "Installing Homebrew..." 
    ruby -e "$(curl -fsSL https://raw.githubusercontent.com/Homebrew/install/master/install)"
  fi
}

dotfiles-all() {
  dotfiles-git
  dotfiles-brew
  dotfiles-python
  dotfiles-node
  dotfiles-tmux
  dotfiles-vim
  dotfiles-zsh
}

dotfiles-main() {
  local cmd=$1

  if [[ -z $DOTFILES_DIRECTORY ]]; then
    echo "dotfiles: DOTFILES_DIRECTORY is not set, execute 'source install.zsh' on the dotfiles directory"
    exit 1
  fi

  cd $DOTFILES_DIRECTORY
  source lib/utils
  source lib/dotfiles-git
  source lib/dotfiles-brew
  source lib/dotfiles-python
  source lib/dotfiles-node
  source lib/dotfiles-tmux
  source lib/dotfiles-zsh
  source lib/dotfiles-vim

  # check that brew exists
  dotfiles-pre

  if  [[ -z $cmd ]] || ! functions "dotfiles-${cmd}" > /dev/null; then
    dotfiles---help
  else
    shift
    "dotfiles-${cmd}" $@
  fi
}

dotfiles-main $@

