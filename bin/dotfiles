#!/bin/zsh

#
#      _       _    __ _ _
#   __| | ___ | |_ / _(_) | ___  ___
#  / _` |/ _ \| __| |_| | |/ _ \/ __|
# | (_| | (_) | |_|  _| | |  __/\__ \
#  \__,_|\___/ \__|_| |_|_|\___||___/
#
# Created on 21/11/15
# Author: Mauricio Poppe <mauricio.poppe@gmail.com>
#

source lib/print

source lib/utils
source lib/macos-utils

dotfiles-help() {
cat << EOF

  usage: dotfiles [<package>|essential|complete]

    [<package>]

      ( platform independent )
        zsh|neovim|tmux
        git|ctags|sshrc

      ( macOS specific )
        macos|homebrew|Iterm2|alfred

      ( Programming languages )
        nodejs|ruby|python|go

    [essential]
      zsh
      tmux
      nvim (requires pip)

    [complete]
      All the installers listed in packages

  Examples:

      dotfiles zsh
      dotfiles nodejs
      dotfiles complete
      dotfiles essential

EOF
}

dotfiles-temp-fix-path() {
  # it's possible that brew is already installed but the path is not set correctly
  # fix the path for the current script
  if [[ ! $PATH == *"/usr/local/bin"* ]]; then
    path=(/usr/local/bin $path)
  fi
  path=(${DOTFILES_DIRECTORY}/bin $path)
}

dotfiles-pre() {
  dotfiles-temp-fix-path

  if ! command-exists "gcc"; then
    print-error "The XCode Command Line Tools must be installed first."
    printf "  Download them from: https://developer.apple.com/downloads\n"
    printf "  Or install them with: xcode-select --install\n"
    exit 1
  fi

  # homebrew
  if ! command-exists "brew"; then
    print-header "Installing Homebrew..."
    ruby -e "$(curl -fsSL https://raw.githubusercontent.com/Homebrew/install/master/install)"
  fi
}

-dotfiles-install() {
  for i in $@; do
    zsh ${i}/install.zsh
  done
}

dotfiles-essential() {
  local list
  list=(zsh tmux neovim)
  -dotfiles-install ${list}
}

dotfiles-complete() {
  dotfiles-essential

  local list
  list=(
    homebrew
    nodejs ruby python go
    git ctags iTerm2 macos alfred
    sshrc
  )
  -dotfiles-install ${list}
}

dotfiles-main() {
  local cmd=$1

  if [[ -z $DOTFILES_DIRECTORY ]]; then
    echo "dotfiles: DOTFILES_DIRECTORY is not set, execute 'source install.zsh' on the dotfiles directory"
    exit 1
  fi

  cd $DOTFILES_DIRECTORY

  dotfiles-pre

  if [[ -z $cmd ]] || [[ $cmd == "--help" ]]; then
    dotfiles-help
  elif [[ -e $cmd/install.zsh ]]; then
    # invoke install.zsh on requested dir
    shift
    source $cmd/install.zsh $@
  elif functions "dotfiles-${cmd}" > /dev/null; then
    shift
    "dotfiles-${cmd}" $@
  else
    dotfiles-help
  fi
}

dotfiles-main $@

