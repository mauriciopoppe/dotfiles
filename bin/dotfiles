#!/bin/zsh

# 
#      _       _    __ _ _           
#   __| | ___ | |_ / _(_) | ___  ___ 
#  / _` |/ _ \| __| |_| | |/ _ \/ __|
# | (_| | (_) | |_|  _| | |  __/\__ \
#  \__,_|\___/ \__|_| |_|_|\___||___/
#                                    
# Created on 21/11/15
# Author: Mauricio Poppe <mauricio.poppe@gmail.com>
#
dotfiles-help() {
cat << EOF

  usage: dotfiles [<package>|essential|complete]

    [<package>]
      One of: zsh|neovim|tmux|git|ctags|osx|homebrew|Iterm2|node|ruby|python|alfred

    [essential]
      zsh
      tmux
      nvim (requires pip)

    [complete]
      All the installers listed above

EOF
}

dotfiles-temp-fix-path() {
  # it's possible that brew is already installed but the path is not set correctly
  # fix the path for the current script
  if [[ ! $PATH == *"/usr/local/bin"* ]]; then
    path=(/usr/local/bin $path)
    export PATH
  fi

  path=(${DOTFILES_DIRECTORY}/bin $path)
}

dotfiles-pre() {
  dotfiles-temp-fix-path

  if ! command-exists "gcc"; then
    print-error "The XCode Command Line Tools must be installed first."
    printf "  Download them from: https://developer.apple.com/downloads\n"
    printf "  Or install them with: xcode-select --install\n"
    exit 1
  fi

  # homebrew
  if ! command-exists "brew"; then
    print-header "Installing Homebrew..." 
    ruby -e "$(curl -fsSL https://raw.githubusercontent.com/Homebrew/install/master/install)"
  fi
}

-dotfiles-install() {
  for i in $@; do
    zsh ${i}/install.zsh
  done
}

dotfiles-essential() {
  local list
  list=(tmux neovim zsh)
  -dotfiles-install ${list}
}

dotfiles-complete() {
  dotfiles-essential

  local list
  list=(
    homebrew 
    node ruby python 
    git ctags iTerm2 osx alfred
  )
  -dotfiles-install ${list}
}

dotfiles-main() {
  local cmd=$1

  if [[ -z $DOTFILES_DIRECTORY ]]; then
    echo "dotfiles: DOTFILES_DIRECTORY is not set, execute 'source install.zsh' on the dotfiles directory"
    exit 1
  fi

  cd $DOTFILES_DIRECTORY
  source lib/utils

  dotfiles-pre

  if [[ -z $cmd ]] || [[ $cmd == "--help" ]]; then
    dotfiles-help
  elif [[ -e $cmd/install.zsh ]]; then
    shift
    zsh $cmd/install.zsh $@
  elif functions "dotfiles-${cmd}" > /dev/null; then
    shift
    "dotfiles-${cmd}" $@
  else
    dotfiles-help
  fi
}

dotfiles-main $@

