#!/bin/zsh
crun() {
  g++ $1 && ./a.out
}

budoLive() {
  budo ${1:=index.js} --live | garnish -- -t babelify
}

# git ignore generator
gi() {
  curl https://www.gitignore.io/api/$@
}

gitignore() {
  gi $@ >> .gitignore
  echo "appending output to .gitignore"
}

################################################################################
# fzf magic functions
# from: https://github.com/junegunn/fzf/wiki/examples
################################################################################
# [f]ile [e]dit
#   
#     $ fe [fuzzy pattern]
#
# Opens the default file with the default editor
#   - Bypass fuzzy finder if there's only one match
#   - Exit if there's no match
#
fe() {
  local file
  file=$(fzf --query="$1" --select-1 --exit-0)
  [[ -n "$file" ]] && ${EDITOR:-vim} "$file"
}

# [f]ile [o]pen
#
#     $ fo [fuzzy pattern]
#
# - Opens a file if <C-o> is pressed
# - Opens a file with $EDITOR if <C-e> is pressed
# 
fo() {
  local out file key
  out=$(fzf-tmux --query="$1" --exit-0 --expect=ctrl-o,ctrl-e)
  key=$(head -1 <<< "$out")
  file=$(head -2 <<< "$out" | tail -1)
  if [[ -n "$file" ]]; then
    [ "$key" = ctrl-o ] && open "$file" || ${EDITOR:-vim} "$file"
  fi
}

################################################################################
# Awesome integration between fasd and fzf
# source http://seanbowman.me/blog/fzf-fasd-and-bash-aliases/
################################################################################
# [j]ump to dir in frecency list
#
#     $ jj [fuzzy pattern]
# 
# Filter in the frecency list generated by fasd
# - frecency list in reverse order
# - replace $HOME by ~            
# - filter with fzf
# - replace ~ with $HOME          
jj() { 
  local dir
  dir=$(fasd -Rdl |\
    sed "s:$HOME:~:" |\
    fzf --no-sort -q "$*" |\
    sed "s:~:$HOME:")
  [[ -d $dir ]] && cd $dir
}

# use ag when indexing
_fzf_compgen_path() {
  ag -g "" "$1"
}

################################################################################
# tmux functions
################################################################################

-tmux-session-fzf() {
  echo $(tmux list-sessions -F "#{session_name}" |\
    fzf-tmux --query="$1" --select-1 --exit-0)
}

# Kills a session
#
#   $ tks [fuzzy pattern]
#
# - Bypass fuzzy finder if there's only one match
# - Exit if there's no match
tks() {
  local session=$(-tmux-session-fzf $@)
  [[ -n $session ]] && tmux kill-session -t "$session"
}

# vim: set ft=zsh
